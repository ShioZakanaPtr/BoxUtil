#version 430

precision highp float;
precision highp int;

layout(local_size_x = RESET_VALUE, local_size_y = 8, local_size_z = 1) in;

subroutine vec4 filteringMode(in ivec2 coord);
subroutine uniform filteringMode filteringModeState;

layout (binding = 0) uniform sampler2D texIn;
layout (binding = 0, rgba8) writeonly uniform image2D texOut;

uniform ivec3 sizeStep;
uniform int vertical;
uniform vec4 uvStepDiv_Lod_ADiv;

float getGaussian(float x) {
	float p = x * uvStepDiv_Lod_ADiv.w;
	return uvStepDiv_Lod_ADiv.w * 0.3989423 * exp(-0.5 * p * p);
}

subroutine(filteringMode) vec4 normalMode(in ivec2 coord) {
	vec2 uv = vec2(coord) / sizeStep.xy;
	vec4 result = vec4(0.0);
	float fi, gaussian, fix = 0.0;
	bool isVertical = bool(vertical);
	for (int i = -sizeStep.z; i <= sizeStep.z; ++i) {
		fi = float(i);
		gaussian = getGaussian(fi);
		fix += gaussian;
		result += textureLod(texIn, fma(uvStepDiv_Lod_ADiv.xy, isVertical ? vec2(0, fi) : vec2(fi, 0), uv), uvStepDiv_Lod_ADiv.z) * gaussian;
	}
	return result / fix;
}

subroutine(filteringMode) vec4 copyMode(in ivec2 coord) {
	return texelFetch(texIn, coord, 0);
}

void main() {
	ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
	if (any(greaterThanEqual(coord, sizeStep.xy))) return;
	imageStore(texOut, coord, filteringModeState(coord));
}
