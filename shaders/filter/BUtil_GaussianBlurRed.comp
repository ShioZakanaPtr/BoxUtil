#version 430

precision OVERWRITE_PRECISION float;
precision highp int;

layout(local_size_x = RESET_VALUE, local_size_y = 8, local_size_z = 1) in;

subroutine void formatPickerStore(in ivec2 coord, in float result);
subroutine uniform formatPickerStore formatPickerStoreState;
subroutine float workMode(in ivec2 coord);
subroutine uniform workMode workModeState;

layout(binding = 0) uniform sampler2D texIn;
layout(binding = 0, r8) uniform writeonly image2D texOut;
layout(binding = 1, r16) uniform writeonly image2D texOut16;

uniform ivec3 sizeStep;
uniform int vertical;
uniform float perStep;

float getGaussian(float x) {
    float p = x * perStep;
    return perStep * 0.3989423 * exp(-0.5 * p * p);
}

subroutine(formatPickerStore) void bit8Store(in ivec2 coord, in float result) {
    imageStore(texOut, coord, vec4(result, vec3(0.0)));
}

subroutine(formatPickerStore) void bit16Store(in ivec2 coord, in float result) {
    imageStore(texOut16, coord, vec4(result, vec3(0.0)));
}

subroutine(workMode) float filterMode(in ivec2 coord) {
    float result = 0.0;
    ivec2 maxIndex = sizeStep.xy - 1;
    ivec2 offset;
    float gaussian, fix = 0.0;
    bool isVertical = bool(vertical);
    for (int i = -sizeStep.z; i <= sizeStep.z; ++i) {
        gaussian = getGaussian(float(i));
        fix += gaussian;
        offset = isVertical ? ivec2(0, i) : ivec2(i, 0);
        result += texelFetch(texIn, clamp(offset + coord, ivec2(0), maxIndex), 0).x * gaussian;
    }
    result /= fix;
    return result;
}

subroutine(workMode) float copyMode(in ivec2 coord) {
    return texelFetch(texIn, coord, 0).x;
}

void main()
{
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(coord, sizeStep.xy))) return;
    float result = workModeState(coord);
    formatPickerStoreState(coord, result);
}